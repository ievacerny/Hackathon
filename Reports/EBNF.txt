#EBNF syntax specification for digital circuit definition file#


specfile = "DEVICES:" , device_list , ";" , "CONNECTIONS:" , connection_list , ";" , [ "MONITOR:" , monitor_list , ";" ] ;



device_list = ( dtype | xor | not | andnandornor | switch | clock | rc ) , { "," , ( dtype | xor | not | andnandornor | switch | clock | rc ) };

dtype = "DTYPE" , name; 

xor = "XOR" , name; 

not = "NOT" , name;

andnandornor = ( "AND" | "NAND" | "OR" | "NOR" ) , name , one_to_sixteen; 

switch = "SWITCH" , name , bool;

clock = "CLOCK" , name , nonzeronumber;

rc = "RC" , name , nonzeronumber;

name = letter , { letter | digit };



connection_list = (name , ["." , dtypeoutput] , "->" , name , "." , input) , { "," , name , ["." , dtypeoutput] , "->" , name , "." , input }; 

dtypeoutput = "Q" | "QBAR"; 

input = ( "I" , one_to_sixteen ) | "DATA" | "CLK" | "SET" | "CLEAR"; 



monitor_list = (name , ["." , dtypeoutput]) , { "," , name , ["." , dtypeoutput]};



bool = "0" | "1"; 

nonzeronumber = nonzerodigit, { digit };

nonzerodigit = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"; 

digit = "0" | nonzerodigit;

one_to_sixteen = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" | "10" | "11" | "12" | "13" | "14" | "15" | "16";

letter = "A" | "B" | "C" | "D" | "E" | "F" | "G"
       | "H" | "I" | "J" | "K" | "L" | "M" | "N"
       | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
       | "V" | "W" | "X" | "Y" | "Z" | "a" | "b"
       | "c" | "d" | "e" | "f" | "g" | "h" | "i"
       | "j" | "k" | "l" | "m" | "n" | "o" | "p"
       | "q" | "r" | "s" | "t" | "u" | "v" | "w"
       | "x" | "y" | "z" ;
