#EBNF syntax specification for digital circuit definition file#

#TODO - CLARIFY AMBIGUITY OF COMMA - WHITESPACE OR ""#


#Section order - final monitor list is optional#

specfile = "DEVICES:" , device_list , ";" , "CONNECTIONS:" , connection_list , ";" , [ "MONITOR:" , monitor_list , ";" ] ;



device_list = { (dtype | xor | andnandornor | switch | clock) , "," };

dtype = "DTYPE" , name; #assume all inputs to (dtype, xor, nand, nor, and, or) are specified by connections to switches in the connection list#

xor = "XOR" , name; 

andnandornor = ( "AND" | "NAND" | "OR" | "NOR" ) , name, one_to_sixteen, "inputs"; 

switch = "SWITCH" , name , bool , "state";

clock = "CLOCK" , name , nonzeronumber , "cycles";

name = letter , (letter | digit | "_" | "-") , { letter | digit | "_" | "-"}; #name must start with letter and have at least one more character after that#



connection_list = { name , "." , output , "->" , name , "." , input , "," }; 

output = "O" | "Q" | "QBAR"; #Q and QBAR for DTYPE, O for all others - validate as semantics#

input = ( "I" , one_to_sixteen ) | "DATA" | "CLK" | "SET" | "CLEAR"; #check proper input name as semantics#


monitor_list = { name , "." , output , "," };



#Primitives#

bool = "0" | "1"; 

nonzeronumber = nonzerodigit, { digit };

nonzerodigit = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"; 

digit = "0" | nonzerodigit;

one_to_sixteen = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" | "10" | "11" | "12" | "13" | "14" | "15" | "16";

letter = "A" | "B" | "C" | "D" | "E" | "F" | "G"
       | "H" | "I" | "J" | "K" | "L" | "M" | "N"
       | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
       | "V" | "W" | "X" | "Y" | "Z" | "a" | "b"
       | "c" | "d" | "e" | "f" | "g" | "h" | "i"
       | "j" | "k" | "l" | "m" | "n" | "o" | "p"
       | "q" | "r" | "s" | "t" | "u" | "v" | "w"
       | "x" | "y" | "z" ;
